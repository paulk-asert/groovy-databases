//////////////// Writing
import groovy.sql.Sql

def url = 'jdbc:hsqldb:mem:marathon'
def user = 'sa'
def password = ''
def driver = 'org.hsqldb.jdbcDriver'
def sql = Sql.newInstance(url, user, password, driver)

def DDL = """
    DROP   TABLE Athlete IF EXISTS;
    CREATE TABLE Athlete (
      athleteId   INTEGER GENERATED BY DEFAULT AS IDENTITY,
      firstname   VARCHAR(64),
      lastname    VARCHAR(64),
      dateOfBirth DATE,
    );
    """

sql.execute(DDL)

sql.execute '''
    INSERT INTO Athlete (firstname, lastname, dateOfBirth)
    VALUES ('Paul', 'Tergat', '1969-06-17')
'''

def data = [first: 'Khalid', last: 'Khannouchi', birth: '1971-12-22']
sql.execute """
    INSERT INTO Athlete (firstname, lastname, dateOfBirth)
    VALUES (${data.first},${data.last},${data.birth})
"""

String athleteInsert = 'INSERT INTO Athlete (firstname, lastname) VALUES (?,?)'
def keys = sql.executeInsert athleteInsert, ['Ronaldo', 'da Costa']
assert keys[0] == [2]
def rowsUpdated = sql.executeUpdate '''
    UPDATE Athlete SET dateOfBirth='1970-06-07' where lastname='da Costa'
'''
assert rowsUpdated == 1
//////////////// End Writing
//////////////// Reading
sql.query('SELECT firstname, lastname FROM Athlete') { resultSet ->
    while (resultSet.next()) {
        print resultSet.getString(1)
        print ' '
        println resultSet.getString('lastname')
    }
}

sql.eachRow('SELECT firstname, lastname FROM Athlete') { row ->
    println row[0] + ' ' + row.lastname
}

def first = sql.firstRow('SELECT lastname, dateOfBirth FROM Athlete')
def firstString = first.toMapString().toLowerCase()
assert firstString == '[lastname:tergat, dateofbirth:1969-06-17]'

List athletes = sql.rows('SELECT firstname, lastname FROM Athlete')
println "There are ${athletes.size()} Athletes:"
println athletes.collect { "$it.FIRSTNAME ${it[-1]}" }.join(", ")

assert sql.firstRow('SELECT COUNT(*) as num FROM Athlete').num == 3
//////////////// End Reading
