//@Grab('org.hsqldb:hsqldb:2.3.2')
//@Grab('org.jodd:jodd-db:3.6')
//@groovy.lang.GrabConfig(systemClassLoader = true)
import groovy.transform.Field
import jodd.db.DbManager
import jodd.db.DbQuery
import jodd.db.DbSession
import jodd.db.pool.CoreConnectionPool

def connectionPool = new CoreConnectionPool(
        driver: 'org.hsqldb.jdbcDriver',
        url: 'jdbc:hsqldb:mem:test',
        user: 'sa',
        password: '')

def DDL = """
    DROP   TABLE Athlete IF EXISTS;
    CREATE TABLE Athlete (
      athleteId   INTEGER GENERATED BY DEFAULT AS IDENTITY,
      firstname   VARCHAR(64),
      lastname    VARCHAR(64),
      dateOfBirth DATE,
    );
"""

connectionPool.init()
DbManager.instance.connectionProvider = connectionPool
def session = new DbSession(connectionPool)
def query = new DbQuery(session, DDL)
query.executeUpdate()

// ordinal params
query = new DbQuery(session, """
INSERT INTO Athlete (firstname, lastname, dateOfBirth)
       VALUES (?,?,?)
""")
query.setString(1, 'Paul')
query.setString(2, 'Tergat')
query.setString(3, '1969-06-17')
query.autoClose().executeUpdate()

// named params
query = new DbQuery(session, """
INSERT INTO Athlete (firstname, lastname, dateOfBirth)
       VALUES (:first,:last,:dob)
""")
query.setString('first', 'Khalid')
query.setString('last', 'Khannouchi')
query.setString('dob', '1971-12-22')
query.autoClose().executeUpdate()

// bean properties
class JoddAthlete {
    String firstname, lastname, dateOfBirth
}
def ja = new JoddAthlete(firstname: 'Ronaldo', lastname: 'da Costa', dateOfBirth: '1970-06-07')
query = new DbQuery(session, """
INSERT INTO Athlete (firstname, lastname, dateOfBirth)
       VALUES (:athlete.firstname,:athlete.lastname,:athlete.dateOfBirth)
""")
query.setBean('athlete', ja)
query.autoClose().executeUpdate()

query = new DbQuery(session, 'select * from ATHLETE')
def rs = query.execute()
def names = []
while(rs.next()) {
    names << "${rs.getString(2)} ${rs.getString(3)}"
}
session.closeSession()
assert names == ['Paul Tergat', 'Khalid Khannouchi', 'Ronaldo da Costa']
